#version 460
#extension GL_EXT_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;

struct pv_t {
  mat4 projection;
  mat4 view;
};
layout(binding = 2, set = 0) uniform upv_t {
  pv_t pv[16];
};

struct Payload {
  vec3 color;
  vec3 normal;
  vec2 uv;
  uint material_id;
  float ao;
  int depth;
};
layout(location = 0) rayPayloadEXT Payload payload;

layout(binding = 3, set = 0) uniform TimeUBO {
  float time;
} ubo;

void main() {
  uint cam = 0;

  mat4 P = pv[cam].projection;
  mat4 V = pv[cam].view;
  mat4 invV = inverse(V);

  float fx = P[0][0];
  float fy = P[1][1];

  vec2 pixel = (vec2(gl_LaunchIDEXT.xy) + 0.5) / vec2(gl_LaunchSizeEXT.xy);
  vec2 ndc = pixel * 2.0 - 1.0;

  vec3 dir_view = normalize(vec3(ndc.x / fx, -ndc.y / fy, -1.0));

  vec3 origin = invV[3].xyz;
  vec3 direction = normalize((invV * vec4(dir_view, 0.0)).xyz);

  payload.color = vec3(0.0);
  payload.normal = vec3(0.0);
  payload.uv = vec2(0.0);
  payload.material_id = 0u;
  payload.ao = 1.0;
  payload.depth = 0;

  traceRayEXT(
    topLevelAS,
    0,
    0xff,
    0, 0, 0,
    origin,
    0.001,
    direction,
    1000.0,
    0
  );

  imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(payload.color, 1.0));
}
